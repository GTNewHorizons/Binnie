buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath "gradle.plugin.se.bjurr.gitchangelog:git-changelog-gradle-plugin:1.47"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.0.6"
}
apply plugin: "se.bjurr.gitchangelog.git-changelog-gradle-plugin"
apply plugin: 'forge'

version = "2.0.30"
if (System.getenv("BUILD_NUMBER") != null) {
    version = version + "." + System.getenv("BUILD_NUMBER")
}
group = "binnie" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "binnie-mods-1.7.10-gtnh"
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// uncomment this hack if you use old IDEA
/*sourceSets {
    main {
        output.resourcesDir = output.classesDir
    }
}*/

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "eclipse"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 
                'mcversion': project.minecraft.version,
                'modid': "BinnieMods"
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task gitChangelogTask(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    filePath = "changelog.html"
    untaggedName = "Current release ${project.version}"
    fromCommit = "dbc5c1a564c39e4a9c042411d1bdbe450985ce2c"
    toRef =  "HEAD"
    templateContent = file('changelog.mustache').getText('UTF-8')
}

curseforge {
    apiKey = project.hasProperty('curseforge_apikey') ? project.curseforge_apikey : '0'
    project {
        id = '223525'
        changelog = file('changelog.html')
        changelogType = 'html'
        releaseType = 'release'
        relations {
            requiredLibrary 'forestry'
            incompatible 'binnie-fix'
            incompatible 'binniepatcher'
            incompatible 'more-binnie-fixes'
        }
    }
}

afterEvaluate {
    tasks.curseforge223525.dependsOn.add(gitChangelogTask)
}
